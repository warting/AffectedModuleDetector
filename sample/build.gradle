import com.dropbox.affectedmoduledetector.AffectedModuleConfiguration
import com.dropbox.sample.Dependencies

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenLocal()
    }
    dependencies {
        classpath Dependencies.Libs.ANDROID_BUILD_TOOLS
        classpath Dependencies.Libs.KOTLIN_GRADLE_PLUGIN
        classpath Dependencies.Libs.KTLINT
        classpath Dependencies.Libs.DETEKT
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt") version "1.20.0"
    id("com.autonomousapps.dependency-analysis") version "1.18.0"

}

apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: "com.dropbox.affectedmoduledetector"

affectedModuleDetector {
    baseDir = "${project.rootDir}"
    pathsAffectingAllModules = [
            "buildSrc/"
    ]
    logFilename = "output.log"
    specifiedBranch = "origin/main"
    compareFrom = "SpecifiedBranchCommitMergeBase"
    customTasks = [
            new AffectedModuleConfiguration.CustomTask(
                    "runDetektByImpact",
                    "detekt",
                    "Run static analysis tool by Impact analysis"
            ),
            new AffectedModuleConfiguration.CustomTask(
                    "runProjectHealthByImpact",
                    "projectHealth",
                    "Run project health analysis tool by Impact"
            )
    ]
    logFolder = "${project.rootDir}"
    excludedModules = [
            "sample-util"
    ]
}

allprojects {
    apply plugin: Dependencies.Libs.DETEKT_PLUGIN

    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
